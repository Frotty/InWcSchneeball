package GameInit
import Child
import EntityManagement
import Gamemode
import PlayerData
import PresentUnwrap
import ClosureTimers
import Collision

function startGame()
	for p in allPlayers
		p.p.addGold(50)
		if p.id > 4
			p.child = new Child(RIGHT_SPAWN.withTerrainZ() + vec3(48. * p.id, 0, 200.), p.p, ORC_CHILD_ID)
		else
			p.child = new Child(LEFT_SPAWN.withTerrainZ() + vec3(48. * p.id, 0, 200), p.p, HUMAN_CHILD_ID)

	if allPlayers.size() == 1
		new Child(vec3(LEFT_SPAWN.x + 48 * 6, LEFT_SPAWN.y, 400.), players[8], ORC_CHILD_ID)
		new Child(vec3(LEFT_SPAWN.x + 48 * 8, LEFT_SPAWN.y, 400.), players[9], ORC_CHILD_ID)
		new Child(vec3(LEFT_SPAWN.x + 48 * 10, LEFT_SPAWN.y, 400.), players[10], ORC_CHILD_ID)
		players[0].addGold(9999)

	SetTimeOfDay(12.)
	doAfter(.5, () -> createDialog())
	doAfter(ANIMATION_PERIOD * 10.5) ->
		CreateTimer().startPeriodic(ANIMATION_PERIOD * 2., () -> globalWorld.update(ANIMATION_PERIOD))

	doPeriodically(2.) cb ->
		for pd in allPlayers
			pd.p.addGold(1)
			if seconds % 6 == 0
				pd.child.actor.addXp(1, true)

	initPresentCircles()
	startEntityLoop()

init
	doAfter(0.1, () -> startGame())

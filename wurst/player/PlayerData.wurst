package PlayerData
import MultiboardClass
import GameConstants
import initlater Gamemode
import ClosureTimers
import Assets
import Players
import initlater Child

public constant allPlayers = new LinkedList<PlayerData>()
public Board playerboard
public PlayerData array pData

public var minutes = 0
public var seconds = 0

public class PlayerData
	int id
	int gameId
	player p
	string name
	string mbname
	string icon
	boolean left = false

	int deaths = 0
	int kills = 0
	int presents = 0

	int killstreak = 0
	boolean indouble = false
	boolean intriple = false

	Child child = null

	construct(player p)
		this.p = p
		id = GetPlayerId(p)
		gameId = allPlayers.size()
		name = p.getNameColored()
		mbname = p.getNameColored()

		allPlayers.add(this)

		if id > 4
			icon = Icons.bTNRaider
		else
			icon = Icons.bTNVillagerKid


	function setMBRow(int r)
		playerboard.setNewItem(r,0,mbname,icon)
		playerboard.setNewItem(r,1,kills.toString(),null)
		playerboard.setNewItem(r,2,deaths.toString(),null)
		playerboard.setNewItem(r,3,presents.toString(),null)

	ondestroy
		child.terminate()
		allPlayers.remove(this)
		pData[p.getId()] = null
		if gameId < allPlayers.size()
			for p2 in allPlayers
				if p2.gameId > gameId
					p2.gameId--
		if playerboard != null
			playerboard.removeRow(gameId + 1)

function initMB()
	playerboard = new Board(allPlayers.size() + 1, 4)

	for i = 0 to allPlayers.size()
		playerboard.board.getItem(i,0)..setStyle(true,true)..setWidth(0.1)
		playerboard.board.getItem(i,1)..setStyle(true,false)..setWidth(0.015)
		playerboard.board.getItem(i,2)..setStyle(true,false)..setWidth(0.015)
		playerboard.board.getItem(i,3)..setStyle(true,false)..setWidth(0.015)

	for pd in allPlayers
		pd.setMBRow(pd.gameId + 1)


	playerboard.board.getItem(0,0)..setStyle(true,false)..setWidth(0.1)
	playerboard.setNewItem(0,0,"Player",null)
	playerboard.board.getItem(0,1)..setStyle(false,true)..setWidth(0.015)
	playerboard.setNewItem(0,1,null,"ReplaceableTextures\\CommandButtons\\BTNSacrifice.blp")
	playerboard.board.getItem(0,2)..setStyle(false,true)..setWidth(0.015)
	playerboard.setNewItem(0,2,null,"ReplaceableTextures\\CommandButtons\\BTNAnkh.blp")
	playerboard.board.getItem(0,3)..setStyle(false,true)..setWidth(0.015)
	playerboard.setNewItem(0,3,null,"ReplaceableTextures\\CommandButtons\\BTNINV_Misc_Gift_01.blp")
	playerboard.board.display(true)

function setTime()
	seconds++

	playerboard.board.setTitle("|cffffffff[|r|cffff6347" + redscore.toString() + "|r - |cff3cb371" + greenscore.toString()
		+ "|r|cffffffff]|r" + COLOR_GOLD.toColorString() + " => " + winscore.toString() + "|r to win | " + MAP_VER)

	if seconds >= 60
		seconds = 0
		minutes++

init
	doAfter(.1) ->
		for p in ALL_PLAYERS
			if p.getSlotState() == PLAYER_SLOT_STATE_PLAYING and p.getController() == MAP_CONTROL_USER
				pData[p.getId()] = new PlayerData(p)

		onPlayerLeave() p ->
			destroy pData[p.getId()]
			printTimed(p.getNameColored() + " has left the game", 15.)

		doAfter(.1, () -> initMB())
		TimerStart(CreateTimer(), 1., true, function setTime)


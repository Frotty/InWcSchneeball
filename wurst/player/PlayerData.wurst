package PlayerData
import MultiboardClass
import LinkedList
import GameConstants
import initlater Gamemode
import ClosureTimers
import Assets

trigger leaveTrig

public constant allPlayers = new LinkedList<PlayerData>()
public Board playerboard
public PlayerData array pData

int minutes = 0
int seconds = 0

public class PlayerData
	int id
	int gameId
	player p
	string name
	string mbname //Basically the output name
	string icon
	boolean left = false

	int deaths = 0
	int kills = 0

	int killstreak = 0
	boolean indouble = false
	boolean intriple = false

	construct (player p)
		this.p = p
		id = GetPlayerId(p)
		gameId = allPlayers.size()
		name = p.getNameColored()
		mbname = p.getNameColored()

		allPlayers.add(this)

		if id > 4
			icon = Icons.bTNRaider
		else
			icon = Icons.bTNVillagerKid


	function setMBRow(int r)
		playerboard.setNewItem(r,0,mbname,icon)
		playerboard.setNewItem(r,1,kills.toString(),null)
		playerboard.setNewItem(r,2,deaths.toString(),null)


	ondestroy
		DisplayTimedTextToPlayer( GetLocalPlayer(), 0, 0, 20., p.getNameColored() + " |cffFFCC33has left the game!")
		playerboard.removeRow(gameId + 1)
		if gameId < allPlayers.size()
			for p2 in allPlayers
				if p2.gameId > gameId
					p2.gameId--
		allPlayers.remove(this)

function initMB()
	playerboard = new Board(allPlayers.size()+1,3)

	for i = 0 to allPlayers.size()
		playerboard.board.getItem(i,0)..setStyle(true,true)..setWidth(0.1)
		playerboard.board.getItem(i,1)..setStyle(true,false)..setWidth(0.015)
		playerboard.board.getItem(i,2)..setStyle(true,false)..setWidth(0.015)

	for pd in allPlayers
		pd.setMBRow(pd.gameId+1)


	playerboard.board.getItem(0,0)..setStyle(true,false)..setWidth(0.1)
	playerboard.setNewItem(0,0,"Player",null)
	playerboard.board.getItem(0,1)..setStyle(false,true)..setWidth(0.015)
	playerboard.setNewItem(0,1,null,"ReplaceableTextures\\CommandButtons\\BTNSacrifice.blp")
	playerboard.board.getItem(0,2)..setStyle(false,true)..setWidth(0.015)
	playerboard.setNewItem(0,2,null,"ReplaceableTextures\\CommandButtons\\BTNAnkh.blp")
	playerboard.board.display(true)

function setTime()
	seconds++

	playerboard.board.setTitle("|cffffffff[|r|cff3cb371" + greenscore.toString() + "|r - |cffff6347" + redscore.toString() + "|r|cffffffff]|r " + MAP_VER)

	if seconds >= 60
		seconds = 0
		minutes++

function playerLeft()
	destroy pData[GetTriggerPlayer().getId()]


init
	leaveTrig = CreateTrigger()
	leaveTrig.addAction(function playerLeft)
	for i = 0 to 11
		if players[i].getSlotState() == PLAYER_SLOT_STATE_PLAYING and players[i].getController() == MAP_CONTROL_USER
			pData[i] = new PlayerData(Player(i))
			leaveTrig.registerPlayerEvent(Player(i), EVENT_PLAYER_LEAVE)
	doAfter(.5, () -> initMB())
	TimerStart(CreateTimer(), 1., true, function setTime)


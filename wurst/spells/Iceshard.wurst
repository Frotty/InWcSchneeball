package Iceshard
import Projectile
import Fx
import initlater Child
import Assets

constant real SPEED = 24

public class Iceshard extends PhysicsProjectile

	construct(vec3 pos, player owner, angle angle)
		super(pos, 32., owner, angle, Abilities.lichMissile)
		fx.setOwner(owner, false)
		setTimed(4.)
		setSpeed(SPEED)
		
	override function update()
		super.update()
		if pos.z < terrainZ
			terminate()
				
		for e in Entity
			if e != this and owner != e.owner and IsPlayerEnemy( e.owner, owner )
				//debugPrint(pos.distToVecSquared(e.pos).toString(),1)
				if pos.distanceToSq(e.pos) < (e.radius+radius).squared()
					onHit(e)
			
	function onHit(Entity e)
		terminate()
		if e instanceof Child
			var c = e castTo Child
			fx.getDummy().damageTarget(c.actor, 65.)
			
	ondestroy
		AddSpecialEffect(Abilities.frostArmorDamage, pos.x, pos.y).destr()
	


	
		

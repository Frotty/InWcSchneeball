package Iceshard
import Projectile
import Fx
import initlater Child
import Assets
import Collision
import AbilityTooltipGenerator

public constant ICESHARD_SPELL_ID = compiletime(ABIL_ID_GEN.next())

constant real SPEED = 26.5

public class Iceshard extends PhysicsProjectile

	construct(vec3 pos, player owner, angle angle)
		super(pos, 28., owner, angle, Abilities.lichMissile)
		fx..setOwner(owner, false)
		..setScale(0.75)
		setTimed(4.)
		setSpeed(SPEED)
		surfaceFriction = 0.981

		registerCollidable(this, (other) -> onHit(other))
		registerCollider(this, SHAPE_SPHERE, (other) -> onHit(other))


	function onHit(Entity e)
		if e.owner.isEnemyOf(owner)
			if e instanceof Child
				var c = e castTo Child
				flashEffect(Objects.critterBloodAlbatross, pos)
				fx.getDummy().damageTarget(c.actor, 25.)
			terminate()

	ondestroy
		AddSpecialEffect(Abilities.frostArmorDamage, pos.x, pos.y).destr()
		removeCollidable(this)
		removeCollider(this)


@compiletime function getObjs()
	let tgen = new AbilityTooltipGenerator("Small, fast ice projectile.")
	new ChannelAbilityPreset(ICESHARD_SPELL_ID, 4, true, tgen)
	..presetButtonPosNormal(3, 2)
	..presetTargetTypes(Targettype.POINT)
	..tooltipStartListen()
	..presetHotkey("R")
	..setName("Ice Shard")
	..presetIcon("BTNIceShard")
	..presetCooldown(lvl -> 1.75 - (lvl / 4))
	..presetManaCost(lvl -> 2 - (lvl / 3).toInt())

	..tooltipStopListen()



package IceWall
import Projectile
import initlater Child
import FxEntity
import Collision
import initlater Snowball
import AbilityTooltipGenerator

public constant ICEWALL_SPELL_ID = compiletime(ABIL_ID_GEN.next())

constant real SPEED = 18.
constant real FACTOR = 2.
constant int COLLOSION = 'e001'

public class IceMissile extends Projectile
	vec3 target

	construct(vec3 pos, player owner, vec3 target)
		super(pos, 32., owner, pos.angleTo2d(target), "Abilities\\Weapons\\WingedSerpentMissile\\WingedSerpentMissile.mdl")
		this.target = target
		setTimed(4.)
		setSpeed(SPEED)

	override function update()
		super.update()
		if pos.toVec2().distanceToSq(target.toVec2()) < 64*64
			terminate()

	function onHit(Entity e)
		terminate()
		if e instanceof Child
			let angle = pos.angleTo2d(e.pos)
			e.addVel(angle.toVec(FACTOR).withZ(1.))


	ondestroy
		for i = -2 to 2
			new Iceblock(pos.toVec2().polarOffset(xyAngle + (22. * i).asAngleDegrees(), 128).toVec3(), owner, .25 )

public class Iceblock extends FxEntity
	real scale
	unit collision
	real timeout = 25.

	construct(vec3 pos, player owner, real scale)
		super(owner, pos, 24, GetRandomReal(0,360).asAngleDegrees(),  "Doodads\\Icecrown\\Rocks\\Icecrown_Crystal\\Icecrown_Crystal" + GetRandomInt(0,8).toString() + ".mdl")
		this.pos.z = pos.getTerrainZ()
		this.scale = scale
		collision = createUnit(owner, COLLOSION, pos, angle(0))
		fx.setScale(scale)
		fx.setZ(0)
		silent = true
		registerCollidable(this, other -> onHit(other))

	override function slowUpdate()
		super.slowUpdate()
		timeout -= 1.
		if timeout <= 0.
			terminate()

	override function update()
		super.update()

	function onHit(Entity other)
		if other instanceof Snowball
			other.terminate()
		scale -= 0.05
		fx.setScale(scale)
		radius -= 6
		if scale <= 0.1
			terminate()

	ondestroy
		collision.remove()
		addEffect("Abilities\\Spells\\Items\\AIma\\AImaTarget.mdl", pos).destr()
		removeCollidable(this)

@compiletime function getObjs()
	let tgen = new AbilityTooltipGenerator("Protect key positions with strategic ice walls.")
	new ChannelAbilityPreset(ICEWALL_SPELL_ID, 4, true, tgen)
	..presetButtonPosNormal(3, 0)
	..presetTargetTypes(Targettype.POINT)
	..tooltipStartListen()
	..presetHotkey("T")
	..setName("Ice wall")
	..presetIcon("BTNIceField")
	..presetCooldown(lvl -> 18.75 - (lvl / 4))
	..presetManaCost(lvl -> 6 - (lvl / 3).toInt())
	..presetCastRange(lvl -> 800.)
	..tooltipStopListen()

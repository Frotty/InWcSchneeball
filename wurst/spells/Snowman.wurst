package Snowman
import HomingProjectile
import Entity
import Fx
import Child
import SoundUtils

constant real SPEED = 8.
constant real MELT = 192

public class SnowmanMissile extends HomingProjectile

	construct(vec3 pos, player owner, vec2 target)
		super(pos, 16., owner, pos.angleTo2d(target), "Abilities\\Weapons\\FrostWyrmMissile\\FrostWyrmMissile.mdl")
		fx.setScale(.7)
		setTimed(6.)
		setSpeed(SPEED)
		throwSound.playOnPoint(pos)
		findTarget()

	function findTarget()
		if target == null
			var closestDist = 2147483640.
			Child closestChild = null
			for c in getEnemyChildren(owner)
				if c.owner.isEnemyOf(owner)
					let dist = pos.distanceToSq(c.pos)
					if dist < closestDist
						closestDist = dist
						closestChild = c
			setTarget(closestChild, HOMING.angular, 0.03)
		
	override function update()
		super.update()
		for child in getEnemyChildren(owner)
			if owner.isEnemyOf(child.owner)
				if pos.distanceToSq(child.getPos()) < (child.radius+radius).squared()
					onHit(child)
					break
			
			
	function onHit(Child child)
		if child.snowman == null
			new SnowmanDummy(child.getPos().add(0,0,-child.terrainZ), owner, child)
		else
			child.snowman.melttime = MELT
		terminate()

		
public class SnowmanDummy extends FxEntity
	real melttime = MELT
	Child target
	
	construct(vec3 pos, player owner, Child target )
		super(pos, 0., owner, target.actor.getFacingAngle(), "Doodads\\Icecrown\\Props\\SnowMan\\SnowMan.mdl")
		fx.setScale(1.4)
		addEffect("Abilities\\Spells\\Human\\Polymorph\\PolyMorphTarget.mdl", pos).destr()
		this.target = target
		target.actor.pause()
		target.snowman = this
		target.actor.setVertexColor(155,155,255,255)
		SetUnitTimeScale(target.actor, 0)
		silent = true
		
	override function update()
		melttime--

		if target.actor.getHP() > .405
			pos = target.pos
			if target.vel.z > 0.
				target.vel.z = 0.
			fx.getDummy().damageTarget(target.actor, .1)
			fx..setColor(colorA(255,255,255,melttime.toInt()+28))
			if melttime < 0
				terminate()
		else
			terminate()

	ondestroy
		PauseUnit(target.actor, false)
		target.actor.setVertexColor(255,255,255,255)
		target.snowman = null
		SetUnitTimeScale(target.actor, 1.)
	
SoundDefinition throwSound

init
	throwSound = new SoundDefinition("Abilities\\Spells\\Other\\FrostBolt\\FrostBoltLaunch1.wav", 1100, false)
		

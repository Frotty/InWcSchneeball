package Snowman
import HomingProjectile
import Entity
import Fx
import Child
import SoundHelper
import SoundQueue

constant real SPEED = 8.
constant real MELT = 192

public class SnowmanMissile extends HomingProjectile

	construct(vec3 pos, player owner, vec2 target)
		super(pos, owner, 16., angleBetweenCoordsR(pos.x, pos.y, target.x, target.y), "Abilities\\Weapons\\FrostWyrmMissile\\FrostWyrmMissile.mdl")
		fx.setOwner(owner, false).setScale(.7)
		real dst = 0.
		Child chld = null
		for c in childs
			if not IsPlayerAlly(c.owner, owner)  and c.owner != owner
				if dst == 0.
					chld = c
					dst = distanceBetweenCoords(c.pos.x, c.pos.y, target.x, target.y)
				else
					let dist = distanceBetweenCoords(c.pos.x, c.pos.y, target.x, target.y)
					if dist < dst
						dst = dist
						chld = c
		setHomingTarget(chld)
		homingType = HOMING.angular
		homingSpeed = 0.03
		setTimed(6.)
		setXYSpeed(SPEED)
		throwSound.playOnPoint(pos)

		
	override function update()
		super.update()
		var chld = target castTo Child

		debugPrint(pos.toString(),1)					
		Entity e = Entity.first
		while e != null
			if e instanceof Child and e != this and owner != e.owner and IsPlayerEnemy( e.owner, owner )
				if pos.distToVecSquared(e.pos) < (e.radius+radius).squared()
					onHit(e)
					
			e = e.next
			
			
	function onHit(Entity e)
		new SnowmanDummy(vec3(pos.x, pos.y, 0.), owner, e castTo Child)
		terminate()

		
class SnowmanDummy extends Entity
	Fx fx
	real melttime = MELT
	Child target
	
	construct(vec3 pos, player owner, Child target )
		super(pos, owner, 0.)
		fx = new Fx(pos, target.actor.getFacing(), "Doodads\\Icecrown\\Props\\SnowMan\\SnowMan.mdl")
		fx.setScale(1.4)
		AddSpecialEffect("Abilities\\Spells\\Human\\Polymorph\\PolyMorphTarget.mdl", pos.x, pos.y).destr()
		this.target = target
		PauseUnit(target.actor, true)
		target.actor.setVertexColor(155,155,255,255)
		SetUnitTimeScale(target.actor, 0)
		
	override function update()
		melttime--

		if target.actor.getHP() > .405
			pos = target.pos
			if target.vel.z > 0.
				target.vel.z = 0.
			fx.setPos(pos.toVec2())
			fx.dummy.damageTarget(target.actor, .1).setVertexColor(255,255,255,melttime.toInt()+28)
			if melttime < 0
				terminate()
		else
			terminate()

	ondestroy
		fx.hiddenDestroy()
		PauseUnit(target.actor, false)
		target.actor.setVertexColor(255,255,255,255)
		SetUnitTimeScale(target.actor, 1.)
	
Sound throwSound

	
init
	throwSound = new Sound("Abilities\\Spells\\Other\\FrostBolt\\FrostBoltLaunch1.wav", 1100, false, true, true, 10, 10, "CombatSoundsEAX")
		

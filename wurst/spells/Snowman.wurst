package Snowman
import HomingProjectile
import Child
import SoundUtils
import FxEntity
import Collision

constant real SPEED = 9.
constant real MELT = 192

constant throwSound = new SoundDefinition("Abilities\\Spells\\Other\\FrostBolt\\FrostBoltLaunch1.wav", false, false)

public class SnowmanMissile extends HomingProjectile

	construct(vec3 pos, player owner, vec2 target)
		super(pos, 24., owner, pos.angleTo2d(target), "Abilities\\Weapons\\FrostWyrmMissile\\FrostWyrmMissile.mdl")
		fx.setScale(.7)
		setTimed(6.)
		setSpeed(SPEED)
		throwSound.playOnPoint(pos)
		findTarget(target)
		registerCollider(this, SHAPE_SPHERE, (e) -> onHit(e))

	function findTarget(vec2 target)
		while this.target == null
			var closestDist = 2147483640.
			Child closestChild = null
			for c in getEnemyChildren(owner)
				if c.owner.isEnemyOf(owner)
					let dist = target.distanceToSq(c.pos.toVec2())
					if dist < closestDist
						closestDist = dist
						closestChild = c
			setTarget(closestChild, HOMING.angular, 0.03)


	function onHit(Entity e)
		if e != null and not e.done and e instanceof Child and e.owner.isEnemyOf(owner)
			let child = e castTo Child
			if child.snowman == null
				new SnowmanDummy(child.getPos().add(0,0,-child.terrainZ), owner, child)
			else
				child.snowman.melttime = MELT
			terminate()

	ondestroy
		removeCollider(this)


public class SnowmanDummy extends FxEntity
	real melttime = MELT
	Child target

	construct(vec3 pos, player owner, Child target )
		super(owner, pos, 0., target.actor.getFacingAngle(), "Doodads\\Icecrown\\Props\\SnowMan\\SnowMan.mdl")
		fx.setScale(1.4)
		addEffect("Abilities\\Spells\\Human\\Polymorph\\PolyMorphTarget.mdl", pos).destr()
		this.target = target
		target.actor.pause()
		target.snowman = this
		target.actor.setVertexColor(155,155,255,255)
		SetUnitTimeScale(target.actor, 0)
		silent = true

	override function update()
		melttime--
		fx..setColor(colorA(255,255,255,melttime.toInt()+28))

		if target.actor.getHP() > .405
			pos = target.pos
			if target.vel.z > 0.
				target.vel.z = 0.
			fx.getDummy().damageTarget(target.actor, .1)
			if melttime < 0
				terminate()
		else
			terminate()


	ondestroy
		PauseUnit(target.actor, false)
		target.actor.setVertexColor(255,255,255,255)
		target.snowman = null
		SetUnitTimeScale(target.actor, 1.)




package Snowball
import Projectile
import Fx
import Child
import SoundUtils
import Assets
import Collision

constant real SPEED = 15.5

public constant throwSound = new SoundDefinition("Abilities\\Spells\\Other\\FrostBolt\\FrostBoltLaunch1.wav", false, false)
public constant terrainHitSound = new SoundDefinition("Abilities\\Spells\\Other\\FrostBolt\\FrostBoltHit1.wav", false, false)
public constant childHitSound = new SoundDefinition("Abilities\\Spells\\Other\\FrostArrows\\FrostArrowHit1.wav", false, false)


public class Snowball extends PhysicsProjectile

	construct(vec3 pos, player owner, angle xyAngle, vec3 target)
		super(pos, 32., owner, xyAngle, "war3mapImported\\snowball.mdx")
		setTimed(2.)
		setFriction(0.905)
		restitution = 0.05
		setTarget(target, SPEED)
		throwSound.playOnPoint(pos)
		registerCollidable(this, (other) -> onHit(other))
		registerCollider(this, SHAPE_SPHERE, (other) -> onHit(other))
		
	function onHit(Entity e)
		if e instanceof Child and e.owner.isEnemyOf(owner)
			var c = e castTo Child
			fx.getDummy().damageTarget(c.actor, 55.)
			childHitSound.playOnPoint(pos)
			terminate()
	
	ondestroy
		AddSpecialEffect(Abilities.frostArmorDamage, pos.x, pos.y).destr()
		removeCollidable(this)
		removeCollider(this)
	
		

package Snowball
import Projectile
import Fx
import Child
import SoundUtils
import Assets
import Collision
import AbilityTooltipGenerator

public constant SNOWBALL_SPELL_ID = compiletime(ABIL_ID_GEN.next())

constant SPEED = 19.5
constant RealLevelClosure RANGE = lvl -> 800. + lvl * 100

public constant throwSound = new SoundDefinition("Abilities\\Spells\\Other\\FrostBolt\\FrostBoltLaunch1.wav", false, false)
public constant terrainHitSound = new SoundDefinition("Abilities\\Spells\\Other\\FrostBolt\\FrostBoltHit1.wav", false, false)
public constant childHitSound = new SoundDefinition("Abilities\\Spells\\Other\\FrostArrows\\FrostArrowHit1.wav", false, false)


public class Snowball extends PhysicsProjectile

	construct(vec3 pos, player owner, angle xyAngle, vec3 target)
		super(pos, 32., owner, xyAngle, "war3mapImported\\snowball.mdx")
		gravity *= 0.15
		setTimed(2.)
		setFriction(0.905)
		restitution = 0.05
		setTarget(pos + xyAngle.toVec(RANGE.run(1)).withTerrainZ(), SPEED)
		throwSound.playOnPoint(pos)
		registerCollidable(this, (other) -> onHit(other))
		registerCollider(this, SHAPE_SPHERE, (other) -> onHit(other))

	function onHit(Entity e)
		if e.owner.isEnemyOf(owner)
			if e instanceof Child
				let child = e castTo Child
				fx.getDummy().damageTarget(child.actor, 55.)
				flashEffect(Abilities.frostArmorDamage, pos)
				flashEffect(Objects.bloodElfSpellThiefBlood, e.getPos())
				childHitSound.playOnPoint(pos)
			terminate()

	override function onGroundHit()
		for child in getEnemyChildren(owner)
			if (child.getPos()  + vec3(0, 0, radius / 2)).distanceToSq(getPos() + vec3(0, 0, radius / 2)) < radius2 + child.radius2
				onHit(child)
		if not done
			flashEffect(Abilities.frostDamage, pos)
			terminate()

	ondestroy
		removeCollidable(this)
		removeCollider(this)

@compiletime function getObjs()
	let tgen = new AbilityTooltipGenerator("An ordinary snowball.")
	new ChannelAbilityPreset(SNOWBALL_SPELL_ID, 4, true, tgen)
	..setHeroAbility(false)
	..presetButtonPosNormal(0, 2)
	..presetTargetTypes(Targettype.POINT)
	..tooltipStartListen()
	..presetHotkey("Q")
	..setName("Snowball")
	..presetIcon("BTNFrostBolt")
	..presetCooldown(lvl -> 1.75 - (lvl / 4))
	..presetManaCost(lvl -> 4 - (lvl / 3).toInt())
	..presetCastRange(RANGE)
	..setLevelSkipRequirement(0)

	..tooltipStopListen()


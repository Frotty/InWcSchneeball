package Rollingball
import Projectile
import Fx
import Child
import Terrain
import SoundHelper

constant real STARTMOMENTUM = 1
constant real MOMENTUMGAIN = .15
constant real MOMENTUMMAX = 20
constant real SIZEGAIN = .075
constant real MAXSIZE = 22.5

public class Rollingball extends Projectile
	real momentum
	real size = 2
	real startz

	construct(vec3 pos, player owner, real angle)
		super(pos, owner, 32.)
		startz = pos.z
		fx = new Fx(pos, angle*RADTODEG, "war3mapImported\\rollingball.mdx")
		fx.setOwner(owner, true)
		xyangle = angle
		setXYSpeed(STARTMOMENTUM)
		momentum = STARTMOMENTUM
		startRoll.playOnPoint(pos)
		hiddenDestroy = true

		
	override function update()
		if momentum < MOMENTUMMAX
			momentum += MOMENTUMGAIN
			setXYSpeed(momentum)
		if size < MAXSIZE
			size += SIZEGAIN
			fx.setScale(size)
			radius = size*8
			pos.z = startz+radius-16.
		super.update()
		var x = polarProjectionXR(pos.x,radius -2., xyangle+90*DEGTORAD )
		var y = polarProjectionYR(pos.y,radius -2., xyangle+90*DEGTORAD )
		//AddSpecialEffect("Abilities\\Weapons\\SpiritOfVengeanceMissile\\SpiritOfVengeanceMissile.mdl", x, y)
		let tz1 = getTerrainZ(x,y)
		x = polarProjectionXR(pos.x,radius -2., xyangle-90*DEGTORAD )
		y = polarProjectionYR(pos.y,radius -2., xyangle-90*DEGTORAD )
		//AddSpecialEffect("Abilities\\Weapons\\SpiritOfVengeanceMissile\\SpiritOfVengeanceMissile.mdl", x, y)
		let tz2 = getTerrainZ(x,y)
		if radius > 32.
			if pos.z < terrainZ or pos.z < tz1 or pos.z < tz2
				terminate()
			else if terrainZ < startz
				startz = terrainZ
			
		Entity e = Entity.first
		while e != null
			if e != this and owner != e.owner and IsPlayerEnemy( e.owner, owner )
				debugPrint(pos.distToVecSquared(e.pos).toString(),1)
				if pos.distToVecSquared(e.pos) < (e.radius+radius).squared()
					debugPrint("inRange",1)
					onHit(e)
					debugPrint("hit",1)
					
			e = e.next
			
			
	function onHit(Entity e)
		if e instanceof Child
			var c = e castTo Child
			terminate()
			fx.dummy.damageTarget(c.actor, momentum*4)
		else if e instanceof Projectile
			e.terminate()
	
	ondestroy
		AddSpecialEffect("war3mapImported\\rollingExplosion.mdx", pos.x, pos.y)
		hitTarget.playOnPoint(pos)
		
Sound startRoll
Sound hitTarget

init
	startRoll = new Sound("Abilities\\Spells\\Orc\\EarthQuake\\EarthquakeRock.wav", 2972, false, true, true, 10, 10, "CombatSoundsEAX")
	hitTarget = new Sound("Abilities\\Spells\\Human\\FlakCannons\\FlakCannon.wav", 1419, false, true, true, 10, 10, "CombatSoundsEAX")
	

				

package Snowbomb
import Projectile
import Fx
import initlater Child
import AbilityTooltipGenerator
import Assets

constant SPEED = 13.5
constant RealLevelClosure AOE = lvl -> (225. + lvl * 25)
constant FACTOR = 6.95

public constant SNOWBOMB_SPELL_ID = compiletime(ABIL_ID_GEN.next())

public class Snowbomb extends PhysicsProjectile
	var lvl = 1

	construct(vec3 pos, player owner, vec3 target, int lvl)
		super(pos, 16., owner, pos.angleTo2d(target), "war3mapImported\\FrostMissile.mdx")
		this.lvl = lvl
		let dist = target.distanceToSq(pos)
		if dist < 512. * 512.
			var ntarget = pos.toVec2().polarOffset(xyAngle, 512).withTerrainZ()
			setTarget(ntarget, SPEED)
		else
			setTarget(target, SPEED)

	override function onGroundHit()
		addEffect("war3mapImported\\IceSparks.mdx", pos)..setScale(1.5).destr()
		for c in getEnemyChildren(owner)
			if pos.distanceToSq(c.getPos()) < AOE.run(lvl).squared()
				let angle = pos.angleTo2d(c.pos)
				let factor = (300 - pos.distanceTo(c.pos)) / 200.
				c.addVel(angle.toVec(factor * FACTOR).withZ(14 * factor))
				fx.getDummy().damageTarget(c.actor, 45 * factor)
				flashEffect(Objects.critterBloodAlbatross, c.getPos())

		terminate()


@compiletime function getObjs()
	let tgen = new AbilityTooltipGenerator("High explosive. Has minimum distance!")
	new ChannelAbilityPreset(SNOWBOMB_SPELL_ID, 4, true, tgen)
	..presetOption(Option.VISIBLE, true)
	..presetOption(Option.TARGETIMAGE, true)
	..presetButtonPosNormal(2, 2)
	..presetTargetTypes(Targettype.POINT)
	..tooltipStartListen()
	..presetHotkey("E")
	..setName("Snowbomb")
	..presetIcon("BTNCRFrostShock")
	..presetCooldown(lvl -> 18. - lvl)
	..presetManaCost(lvl -> 5)
	..presetCastRange(lvl -> 700. + 200 * lvl)
	..presetAreaofEffect(AOE)

	..tooltipStopListen()

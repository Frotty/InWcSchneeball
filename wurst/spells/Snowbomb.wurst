package Snowbomb
import Projectile
import Fx
import initlater Child
import AbilityTooltipGenerator
import Assets

constant SPEED = 13.5
constant RealLevelClosure AOE = lvl -> (275. + lvl * 32)
constant FACTOR = 6.95

public constant SNOWBOMB_SPELL_ID = compiletime(ABIL_ID_GEN.next())

public class Snowbomb extends PhysicsProjectile
	var lvl = 1

	construct(vec3 pos, player owner, vec3 target)
		super(pos, 16., owner, pos.angleTo2d(target), "war3mapImported\\FrostMissile.mdx")
		let dist = target.distanceToSq(pos)
		if dist < 512.
			var ntarget = target.toVec2().polarOffset(xyAngle, 200.-dist).toVec3()
			setTarget(ntarget, SPEED)
		else
			setTarget(target, SPEED)

	override function onGroundHit()
		addEffect("war3mapImported\\IceSparks.mdx", pos)..setScale(1.5).destr()
		for c in getEnemyChildren(owner)
			if pos.distanceToSq(c.getPos()) < AOE.run(lvl).squared()
				let angle = pos.angleTo2d(c.pos)
				let factor = (300 - pos.distanceTo(c.pos)) / 200.
				c.addVel(angle.toVec(factor * FACTOR).withZ(14*factor))
				fx.getDummy().damageTarget(c.actor, 65 * factor)
				flashEffect(Objects.critterBloodAlbatross, c.getPos())

		terminate()


@compiletime function getObjs()
	let tgen = new AbilityTooltipGenerator("High explosive. Has minimum distance!")
	new ChannelAbilityPreset(SNOWBOMB_SPELL_ID, 4, true, tgen)
	..presetOption(Option.VISIBLE, true)
	..presetOption(Option.TARGETIMAGE, true)
	..presetButtonPosNormal(2, 2)
	..presetTargetTypes(Targettype.POINT)
	..tooltipStartListen()
	..presetHotkey("E")
	..setName("Snowbomb")
	..presetIcon("BTNCRFrostShock")
	..presetCooldown(lvl -> 16 - (lvl / 2))
	..presetManaCost(lvl -> 5)
	..presetCastRange(lvl -> 700. + 200 * lvl)
	..presetAreaofEffect(AOE)

	..tooltipStopListen()

package KillStreaks
import PlayerData
import GameConstants
import Gamemode
import TimerUtils
import ClosureTimers

public function adjustKills(player killer, player dead)
	var p1 = pData[killer.getId()]
	var p2 = pData[dead.getId()]
	p1.kills++
	p2.deaths++
	playerboard.updateValue(p1.gameId + 1, 1, p1.kills.toString())
	playerboard.updateValue(p2.gameId + 1, 2, p2.deaths.toString())
	killer.addGold(10)
	p1.killstreak++
	p2.killstreak = 0
	if p1.id <= 4
		if redscore < winscore - 1
			redscore += 1
	else
		if greenscore < winscore - 1
			greenscore += 1

	string announce = " -"
	if p1.killstreak == 3
		announcers[3].play()
		announce += " |cff860B00KillingSpree|r"
	if p1.killstreak == 4
		announcers[4].play()
		announce += " |cff0058A6Unstoppable!|r"
	if p1.killstreak == 6
		announcers[6].play()
		announce += " |cffFFDA84Godlike!!!|r"
	if p1.killstreak > 6
		announcers[8].play()
		announce += " |cff11DA84Beyond Godlike!!!|r"
	timer t = getTimer()
	if p1.intriple
		announcers[1].play()
		announce += " |cff2DCB2DTriplekill|r"
		p1.indouble = false
		t..setData(killer.getId())..start(0.,function clearKillMultiplier)
	else if p1.indouble
		announcers[0].play()
		announce += " |cff2DCB2DDoublekill|r"
		p1.intriple = true
		t..setData(killer.getId())..start(15,function clearKillMultiplier)
	else
		t..setData(killer.getId())..start(20,function clearKillMultiplier)
		p1.indouble = true
	if announce != " -"
		DisplayTimedTextToForce(GetPlayersAll(), 12., killer.getNameColored() + " hit " + dead.getNameColored() + "|r in the face with a snowball!" + announce + " (|cffFFCC00+1|r)")
	else
		DisplayTimedTextToForce(GetPlayersAll(), 12., killer.getNameColored() + " hit " + dead.getNameColored() + "|r in the face with a snowball! (|cffFFCC00+1|r)")

	doAfter(2.5) ->
		checkEnd()


function clearKillMultiplier()
	var p = pData[GetExpiredTimer().getData()]
	if p.indouble
		p.indouble = false
	else
		p.intriple = false

package Presents
import SoundUtils
import Entity
import Child
import ClosureTimers

constant int PRESENT_ID = 'I002'

let presentSpawn = new SoundDefinition("war3mapImported\\bells.mp3", false)
let treeCenter = vec2(0., 4096.)

public class Present extends ItemEntity
	Child holder

	construct(vec2 pos)
		super(createItem(PRESENT_ID, pos))

	override function update()

	override function onPickup(Entity e)
		if e instanceof Child
			let child = e castTo Child
			if child.actor.itemCount(PRESENT_ID) > 1
				setNewActor(createItem(PRESENT_ID, getPos()))
				createTTEx(child.actor.getPos3Real(), vec2(0,0.05), "|cffcccc00You can only carry one!|r", 10,  2., colorA(255,255,255,0), child.owner )
			else
				printTimed(child.owner.getNameColored() + " has acquired a present!", 12.)
				holder = child
				child.hasPresent = true

	override function onDrop(Entity e)
		if e instanceof Child
			(e castTo Child).hasPresent = false
			holder = null

	override function onUse(Entity e)

	ondestroy
		holder.hasPresent = false

public function spawnPresent()
	var randomPos = treeCenter.polarOffset(GetRandomReal(1,360).asAngleDegrees(), GetRandomReal(300,700))
	new Present(randomPos)
	if GetRandomInt(0, 4) > 1
		doAfter(GetRandomReal(10, 20)) ->
			randomPos = treeCenter.polarOffset(GetRandomReal(1,360).asAngleDegrees(), GetRandomReal(300,700))
			createItem('texp', randomPos)
	if GetRandomInt(0, 4) > 3
		doAfter(GetRandomReal(20, 30)) ->
			randomPos = treeCenter.polarOffset(GetRandomReal(1,360).asAngleDegrees(), GetRandomReal(300,700))
			createItem('texp', randomPos)

	printTimed("|cffFFAD29Â» |cffFF7329Present spawned in the middle!", 12)

function isPresent() returns boolean
	return GetItemTypeId(GetFilterItem()) == PRESENT_ID

int presents

function addOne()
	presents++

public function onPresentTime()
	printLog(Loglevel.DEBUG, "On Present Time")
	presents = 0
	rect r = Rect(-6000,-19000,6000,10000)
	EnumItemsInRect(r, Condition(function isPresent), function addOne)
	timer t = getTimer()
	t.start(0.01,function checkPresents)

function checkPresents()
	if presents <= 2
		spawnPresent()
		presentSpawn.play()

init
	timer t = getTimer()
	t.startPeriodic(30.,function onPresentTime)


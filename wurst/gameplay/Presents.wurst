package Presents
import SoundUtils
import Entity
import Child
import ClosureTimers
import Assets
import GameConstants

constant int PRESENT_ID = 'I002'

let presentSpawn = new SoundDefinition("war3mapImported\\bells.mp3", false)
let treeCenter = vec2(0., 4096.)
let itemRect = Rect(-6000,-19000,6000,10000)

public class Present extends ItemEntity
	Child holder

	construct(vec2 pos)
		super(createItem(PRESENT_ID, pos))
		flashEffect(Abilities.reviveHuman1, pos)

	override function update()

	override function onPickup(Entity e)
		if e instanceof Child
			let child = e castTo Child
			if child.actor.itemCount(PRESENT_ID) > 1
				setNewActor(createItem(PRESENT_ID, getPos()))
				createTTEx(child.actor.getPos3Real(), vec2(0,0.05), "|cffcccc00You can only carry one!|r", 10,  2., colorA(255,255,255,0), child.owner )
			else
				printTimed(child.owner.getNameColored() + " has acquired a present!", 12.)
				holder = child
				child.hasPresent = true

	override function onDrop(Entity e)
		if e instanceof Child
			let child = (e castTo Child)
			child.hasPresent = false
			holder = null
			if not child.actor.isAlive() and
				(child.getPos().distanceTo(gg_unit_ncop_0021.getPos3Real()) < 256. or child.getPos().distanceTo(gg_unit_ncop_0024.getPos3Real()) < 256.)
				announcers[7].play()

	override function onUse(Entity e)

	ondestroy
		holder.hasPresent = false

public function spawnPresent()
	var randomPos = treeCenter.polarOffset(GetRandomReal(1,360).fromDeg(), GetRandomReal(300,750))
	new Present(randomPos)
	if GetRandomInt(0, 4) > 1
		doAfter(GetRandomReal(10, 20)) ->
			randomPos = treeCenter.polarOffset(GetRandomReal(1,360).fromDeg(), GetRandomReal(300,750))
			createItem('texp', randomPos)
	if GetRandomInt(0, 4) > 3
		doAfter(GetRandomReal(20, 30)) ->
			randomPos = treeCenter.polarOffset(GetRandomReal(1,360).fromDeg(), GetRandomReal(300,750))
			createItem('texp', randomPos)

	printTimed("|cffFFAD29Â» |cffFF7329Present spawned in the middle!", 12)
	presentSpawn.play()

function isPresent() returns boolean
	return GetItemTypeId(GetFilterItem()) == PRESENT_ID

int presents

function addOne()
	presents++

public function onPresentTime()
	printLog(Loglevel.DEBUG, "On Present Time")
	presents = 0
	EnumItemsInRect(itemRect, Condition(function isPresent), function addOne)

	nullTimer() ->
		if presents <= 2
			spawnPresent()

let CD_SOUND = new SoundDefinition("321.mp3", false)
let BEGIN_SOUND = new SoundDefinition(Sounds.clanInvitation, false)
let WARMUP_SOUND = new SoundDefinition(Sounds.newTournament, false)

init
	doAfter(1) ->
		WARMUP_SOUND.play()
		let t = getTimer()
		let dia = t.createTimerDialog()..setTitle("Warmup")
		dia.display(true)
		doAfter(17) ->
			CD_SOUND.play()
		t.doAfter(20) ->
			BEGIN_SOUND.play()
			dia.destr()
			onPresentTime()
			doPeriodically(30) cb ->
				onPresentTime()

package Projectile
import public Entity
import Fx
import Terrain
import PhysicsConstants

public class Projectile extends Entity
	Fx fx
	boolean timed = false
	real time
	real xyspeed
	real xyangle
	boolean keepHeight = true
	real terrainZ = 0.
	boolean gravityAffected = false
	boolean hiddenDestroy = false
	
	construct(vec3 pos, player owner, real radius)
		super(pos, owner, radius)
		
	function setTimed(real duration)
		timed = true
		time = duration		
		
	function setXYSpeed(real speed)
		xyspeed = speed
		vel = vec3(Cos(xyangle)*speed, Sin(xyangle)*speed, vel.z)
		
	override function update()
		terrainZ = getTerrainZ(pos.x, pos.y)
		if gravityAffected
			vel += gravity
		pos += vel
		if keepHeight
			fx.setPos(pos.x, pos.y, pos.z-terrainZ)
		else
			fx.setPos(pos)
		fx.setXYAngle(xyangle*RADTODEG)
		if timed
			time -= ANIMATION_PERIOD
			if time < 0
				terminate()
				
				
	ondestroy
		if hiddenDestroy
			fx.hiddenDestroy()
		else
			destroy fx
		
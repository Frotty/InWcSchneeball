package TimedBar
import Entity
import CustomBar
import public Colors
import initlater PresentUnwrap
import initlater Child

public class TimedBar extends Entity
	CustomBar bar
	Child child
	real cvalue = 0
	real time
	unit u
	boolean correct = false

	construct(Child child, string char, int chars, real value, real time, colorA bg, colorA fg, unit u)
		super(DUMMY_PLAYER, child.getPos() + vec3(-60., 0, -16), 0.)
		this.child = child
		this.u = u
		u.pause()
		this.time = time
		bar = new CustomBar(pos,char, chars, 5)
		bar..setValue(value)
		..setBackground(bg)
		..setForeground(fg)

	override function update()
		cvalue += ANIMATION_PERIOD
		bar.setValue(cvalue/time*100)
		let tpos = child.getPos().toVec2() + vec2(-60., 0)
		bar.setPosXY(tpos.x, tpos.y)
		if cvalue >= time
			correct = true
			terminate()

	ondestroy
		checkUnwrap(u)
		u.unpause()
		(u.getEntity() castTo Child).wrapBar = null
		destroy bar

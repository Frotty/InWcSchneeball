package PresentUnwrap
import Gamemode
import Entity
import TimedBar
import Child
import GameConstants
import ClosureTimers
import Assets

constant PRESENT_ID = 'I002'
constant UNWRAP_ID = 'A007'
constant CIRCLE_ID = 'ncop'

constant PRESENTS = ['I000', 'I001', 'I00B', 'I00C', 'I00D']

public function checkUnwrap(unit u)
	for int i = 0 to 5
		var uit = UnitItemInSlot(u, i)
		if GetItemTypeId(uit) == PRESENT_ID
			uit.getEntity().terminate()
			u.addItemById(PRESENTS[GetRandomInt(1, 4)])
			break

function isChild() returns boolean
	return GetFilterUnit().getEntity() instanceof Child

function sellPresent()
	let u = GetTriggerUnit()
	let p = GetOwningPlayer(u)
	for int i = 0 to 5
		var uit = UnitItemInSlot(u, i)
		if GetItemTypeId(uit) == PRESENT_ID
			uit.getEntity().terminate()
			p.addGold(100)
			createTTEx(vec3(u.getX(), u.getY(), 20.), vec2(0, 0.05), "|cffffcc00+100|r", 10,  2., colorA(255,255,255,0), p)
			gg_unit_e002_0011..setAnimation("Spell")..queueAnimation("Walk - 2")
			gg_unit_e002_0012..setAnimation("Spell")..queueAnimation("Walk - 2")
			announcers[2].play()
			DisplayTimedTextToForce(GetPlayersAll(), 12., "|cffFFCC00>>>|r " + p.getNameColored() + "|r has returned a Present gaining |cffFFCC00+3|r Points for his team!")
			if p.getId() <= 4
				redscore += 3
			else
				greenscore += 3

			for child in getEnemyChildren(p)
				child.actor.addXp(25, true)

			doAfter(2.5) ->
				checkEnd()


function startBar()
	var u = GetSpellAbilityUnit()
	var id = GetSpellAbilityId()
	if id == UNWRAP_ID
		let child = (u.getEntity() castTo Child)
		child.wrapBar = new TimedBar(vec3(u.getX() - 60., u.getY(), -16), "|", 120, 0, 7.5, COLOR_WHITE, COLOR_GOLD, u)


constant RUNES = ['rhe1', 'rman', 'rspd', 'gold']

class RuneItem extends ItemEntity

	construct(vec2 pos)
		super(createItem(RUNES[GetRandomInt(0, 4)], pos))
		flashEffect(Abilities.healTarget, pos)

	override function onPickup(UnitEntity entity)
		let tpos = getPos()
		doAfter(GetRandomReal(30, 45)) ->
			new RuneItem(tpos.toVec2())
		terminate()


	override function onDrop(UnitEntity entity)

	override function onUse(UnitEntity entity)

public function initPowerupCircles()
	gg_unit_ncop_0022..setVertexColor(color(105, 195, 115))..setScale(0.55)
	gg_unit_ncop_0023..setVertexColor(color(105, 195, 115))..setScale(0.55)

	doAfter(GetRandomReal(30, 45)) ->
		new RuneItem(gg_unit_ncop_0022.getPos())

	doAfter(GetRandomReal(30, 45)) ->
		new RuneItem(gg_unit_ncop_0023.getPos())


public function initPresentCircles()
	gg_unit_ncop_0021..setVertexColor(COLOR_GOLD)
	gg_unit_ncop_0024..setVertexColor(COLOR_GOLD)
	CreateTrigger()
	..registerUnitInRange(gg_unit_ncop_0021, 100, Condition(function isChild))
	..addAction(function sellPresent)
	CreateTrigger()
	..registerUnitInRange(gg_unit_ncop_0024, 100, Condition(function isChild))
	..addAction(function sellPresent)

init
	EventListener.add(EVENT_PLAYER_UNIT_SPELL_CHANNEL) ->
		startBar()

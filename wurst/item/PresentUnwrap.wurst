package PresentUnwrap
import Gamemode
import Entity
import TimedBar
import Child
import GameConstants
import ClosureTimers

constant PRESENT_ID = 'I002'
constant UNWRAP_ID = 'A007'
constant CIRCLE_ID = 'ncop'

int array PRESENTS

public function checkUnwrap(unit u)
	for int i = 0 to 5
		var uit = UnitItemInSlot(u, i)
		if GetItemTypeId(uit) == PRESENT_ID
			uit.getEntity().terminate()
			u.addItemById(PRESENTS[GetRandomInt(1, 4)])
			break

function isChild() returns boolean
	return GetFilterUnit().getEntity() instanceof Child

function sellPresent()
	let u = GetTriggerUnit()
	let p = GetOwningPlayer(u)
	for int i = 0 to 5
		var uit = UnitItemInSlot(u, i)
		if GetItemTypeId(uit) == PRESENT_ID
			uit.getEntity().terminate()
			p.addGold(100)
			createTTEx(vec3(u.getX(), u.getY(), 20.), vec2(0, 0.05), "|cffffcc00+100|r", 10,  2., colorA(255,255,255,0), p)
			gg_unit_e002_0011..setAnimation("Spell")..queueAnimation("Walk - 2")
			gg_unit_e002_0012..setAnimation("Spell")..queueAnimation("Walk - 2")
			announcers[2].play()
			DisplayTimedTextToForce(GetPlayersAll(), 12., "|cffFFCC00>>>|r " + p.getNameColored() + "|r has returned a Present gaining |cffFFCC00+3|r Points for his team!")
			if p.getId() <= 5
				redscore += 3
			else
				greenscore += 3

			doAfter(2.) ->
				checkEnd()


function startBar()
	var u = GetSpellAbilityUnit()
	var id = GetSpellAbilityId()
	if id == UNWRAP_ID
		let child = (u.getEntity() castTo Child)
		child.wrapBar = new TimedBar(vec3(u.getX()-60.,u.getY(),-16), "|", 120, 0, 7.5, COLOR_WHITE, COLOR_GOLD, u)


trigger t
unit scircle
unit ncircle
public function initPresentCircles()
	scircle = CreateUnit(players[PLAYER_NEUTRAL_PASSIVE], CIRCLE_ID, -256, 1350, 0.)..setVertexColor(COLOR_GOLD)
	ncircle = CreateUnit(players[PLAYER_NEUTRAL_PASSIVE], CIRCLE_ID, 0, 6540, 0.)..setVertexColor(COLOR_GOLD)
	t = CreateTrigger()
	t.registerUnitInRange(scircle, 100,Condition(function isChild))
	t.addAction(function sellPresent)
	t = CreateTrigger()
	t.registerUnitInRange(ncircle, 100,Condition(function isChild))
	t.addAction(function sellPresent)

init
	t = CreateTrigger()
	t.registerAnyUnitEvent(EVENT_PLAYER_UNIT_SPELL_CHANNEL)
	t.addAction(function startBar)
	PRESENTS[0] = 'I000'
	PRESENTS[1] = 'I001'
	PRESENTS[2] = 'I00B'
	PRESENTS[3] = 'I00C'
	PRESENTS[4] = 'I00D'
